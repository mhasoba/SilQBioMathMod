# the linear planar systems
# planar.ode
x'=a*x+b*y
y'=c*x+d*y
par a=-1,b=0,c=0,d=-2
init x=2,y=0
@ xp=x,yp=y,xlo=-5,ylo=-5,xhi=5,yhi=5
# here is a tutorial
"       Linear planar systems
" There are several different behaviors for the phase-plane of a linear 2D system
" To see these click on the (*) and then DirField Flow 5 
" {a=-1,b=0,c=0,d=-2} 1. Stable node - two real negative eigenvalues 
" {a=0,b=1,c=3,d=0} 2. Saddle point - a positive and negative eigenvalue
" {a=-1,b=3,c=-2,d=0} 3. Stable vortex - complex eigenvalues with negative real parts
" {a=.5,b=2,c=-2,d=-.25} 4. Unstable vortex - complex with positive real parts 
" {a=.5,b=0,c=-.5,d=.5} 5. Unstable node - two positive eigenvalues
" {a=.5,b=2,c=-2,d=-.5} 6. Center - inaginary eigenvalues
" {a=-1,b=1,c=2,d=-2} 7. Zero eigenvalue
"  
" Try your own values and classify them!
done




